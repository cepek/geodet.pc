C RESENI PREURCENE SOUSTAVY A*X=B GIVENSOVOU TRANSFORMACI
C=======================================================================
C
      SUBROUTINE GIVENS(MODE,LUN,M,MAXM,TOLMIN,TOLEPS,DEFTR,M0,DEFEKT,
     /                  NX,INDX,IOUT)
C
C VSTUPNI PARAMETRY:
C MODE    SPECIFIKACE VARIANTY RESENI:
C         MODE = 1  RESENI REGULARNI SOUSTAVY NEBO AUTOMATICKA 
C                   REGULARIZACE
C         MODE = 2  RESENI RIZENE REGULARIZACE (MUSI PREDCHAZET VOLANI
C                   S HODNOTOU MODE=1)
C         MODE = 3  RESENI REGULARNI SOUSTAVY NEBO RESENI SINGULARNI
C                   SOUSTAVY S RIZENOU REGULARIZACI (JE EKVIVALENTNI
C                   DVOJIMU VOLANI S HODNOTOU MODE=1 A 2)
C LUN     CISLO LOGICKEHO ZARIZENI PRO CTENI MATICE SOUSTAVY
C         (PRO CTENI MATICE JE VOLANA PROCEDURA GTGETR)
C M       PRACOVNI PROSTOR, VE KTEREM JSOU ALOKOVANA VSECHNA POTREBNA
C         PRACOVNI POLE
C TOLMIN  TOLERANCE PRO IDENTIFIKACI LINEARNE ZAVISLYCH SLOUPCU
C         (HODNOTY PRVKU HLAVNI DIAGONALY, RESP. NORMY VEKTORU
C         PRI RESENI SINGULARNI SOUSTAVY)
C TOLEPS  CHARAKTERISTIKA SKUTECNE PRESNOSTI PRVKU MATICE SOUSTAVY
C         (TOLERANCE PRO IDENTIFIKACI NULOVYCH PRVKU PRI VYPOCTU 
C         TRANSFORMACNICH KOEFICIENTU)
C DEFTR   POCET NEZNAMYCH PRO TRANSFORMACI SINGULARNI SOUSTAVY
C NX      POCET PRVKU SUBVEKTORU NEZNAMYCH, JEHOZ DELKA MA BYT
C         MINIMALIZOVANA
C INDX    POLE PRO REGISTRACI INDEXU NEZNAMYCH, KTERE VSTUPUJI DO
C         MINIMALIZACNI PODMINKY V PRIPADE NENULOVEHO DEFEKTU
C
C VSTUPNI/VYSTUPNI PARAMETR:
C MAXM  NA VSTUPU DIMENZE POLE M, NA VYSTUPU POCET OBSAZENYCH PRVKU
C
C VYSTUPNI PARAMETRY:
C M0      EMPIRICKA JEDNOTKOVA STREDNI CHYBA
C DEFEKT  DEFEKT SOUSTAVY
C IOUT    IOUT = 0  NORMALNI UKONCENI PROCEDURY
C         IOUT < 0  NEDOSTATECNA PAMET NEBO CHYBA PRI CTENI MATICE  A
C
C-----------------------------------------------------------------------
C
      INTEGER  MODE, LUN, MAXM, DEFTR, DEFEKT, NX, INDX(1), IOUT
C$INCLUDE: 'GTREAL.INC'
      DOUBLE PRECISION
     /         M(1), TOLMIN, TOLEPS, M0
C
C-----------------------------------------------------------------------
C
C     *********   POPIS OBSAZENI PRACOVNIHO POLE  M   *********
C
C IMX     NA VYSTUPU VEKTOR NEZNAMYCH X (PRI ZPRACOVANI VEKTOR B)
C IMADJ   STRUKTURA SOUSEDNOSTI MATICE TRANS(A)*A
C IMPERM  VEKTOR PERMUTACE NEZNAMYCH
C IMINVP  INVERZNI PERMUTACE
C IMXRNZ  SMERNIKY SEZNAMU NENULOVYCH PRVKU ULOZENYCH VE VEKTORU RNZ
C IMXNZS  VEKTOR SMERNIKU  XNZSUB  NA KOMPRIMOVANY SEZNAM INDEXU
C IMNZS   VEKTOR  NZSUB  KOMPRIMOVANEHO SEZNAMU INDEXU
C IMDIAG  VEKTOR DIAGONALNICH PRVKU
C IMRAD   PRACOVNI PROSTOR PRO ULOZENI BEZNEHO RADKU
C IMRMAS  MASKA POUZITYCH RADKU MATICE R
C IMVAHA  VEKTOR VAH
C IMRNEP  VEKTOR NENULOVYCH PRVKU MATICE ROZKLADU R
C IMINDC  VEKTOR INDEXU NENULOVYCH PRVKU BEZNEHO RADKU
C IMROW   VEKTOR NENULOVYCH PRVKU BEZNEHO RADKU
C IMDFSZ  SEZNAM INDEXU LINEARNE ZAVISLYCH SLOUPCU
C IMDFMK  MATICE K SINGULARNI SOUSTAVY - DIMENZE:  K(MDFMK,NDFMK)
C IMFREE  UKAZATEL NA VOLNOU OBLAST POLE M
C IMMAXR  POCET PRVKU TYPU REAL, KTERE LZE ULOZIT DO PRACOVNI OBLASTI
C IMMAXI  POCET PRVKU TYPU INTEGER, KTERE LZE ULOZIT DO PRACOVNI OBLASTI
C
C-----------------------------------------------------------------------
C
C$INCLUDE: 'GTIMPT.INC'
      INTEGER         
     /   IMX   , IMADJ , IMPERM, IMINVP, IMXRNZ, IMXNZS, IMNZS , IMDIAG,
     /   IMRAD , IMRMAS, IMVAHA, IMRNEP, IMINDC, IMROW , IMDFSZ, 
     /   IMDFMK, MDFMK , NDFMK ,
     /   IMFREE, IMMAXR, IMMAXI
      COMMON /GTIMPT/
     /   IMX   , IMADJ , IMPERM, IMINVP, IMXRNZ, IMXNZS, IMNZS , IMDIAG,
     /   IMRAD , IMRMAS, IMVAHA, IMRNEP, IMINDC, IMROW , IMDFSZ, 
     /   IMDFMK, MDFMK , NDFMK ,
     /   IMFREE, IMMAXR, IMMAXI
C
      INTEGER  GTRINT, GTINTR
      INTEGER  POCNEZ, POCPOZ, MAXADJ, OUTP, MAXNZS, POCNEP, DIMROW,
     /         PIMFR, PMAXM, RPOC, TMPADJ, MAXTMP, INTMP
      INTEGER  I, J, K, IDF
C
      IF(MODE.EQ.2)  GOTO 2
C
C     ------------
C     INICIALIZACE
C     ------------
      CALL GTTIME
      CALL GTGET0(LUN,POCNEZ,POCPOZ,OUTP)
      IMFREE = 1
      IMMAXR = MAXM
      IMMAXI = GTRINT(MAXM)
      OUTP   = 0
      CALL GTFREI(POCNEZ,IMX   ,IMFREE,MAXM,OUTP)
      IOUT = OUTP - 10
      IF(OUTP.NE.0)  RETURN
      CALL GTFREI(POCNEZ  ,IMPERM,IMFREE,MAXM,OUTP)
      CALL GTFREI(POCNEZ  ,IMINVP,IMFREE,MAXM,OUTP)
      CALL GTFREI(POCNEZ+1,IMXRNZ,IMFREE,MAXM,OUTP)
      CALL GTFREI(POCNEZ+1,IMXNZS,IMFREE,MAXM,OUTP)
C     -------------------------------
C     VYTVORENI STRUKTURY SOUSEDNOSTI 
C     -------------------------------
      MAXADJ = GTRINT(MAXM)
      CALL GTVADJ(LUN,M(IMFREE),MAXADJ,OUTP)
      IOUT = OUTP - 20
      IF(OUTP.NE.0)  RETURN
      RPOC  = GTINTR(MAXADJ)
      TMPADJ = IMFREE + MAXM - RPOC
      K = IMFREE + RPOC - 1
      IF(K.GT.MAXM)  OUTP = -1
      J = IMFREE + MAXM - 1
      MAXM = MAXM - RPOC
      CALL GTFREI(MAXADJ,IMADJ,IMFREE,MAXM,OUTP)
      IOUT = OUTP - 20
      IF(OUTP.NE.0)  RETURN
C     ------------------------------------------------------------
C     NAKOPIROVANI STRUKTURY SOUSEDNOSTI NA KONEC PRACOVNI OBLASTI
C     ------------------------------------------------------------
      DO 100 I=1,RPOC
         M(J) = M(K)
         J = J - 1
         K = K - 1
100   CONTINUE
      CALL GTTIME
C     -------------------------------------------------
C     PERMUTACE NEZNAMYCH (ZNICI STRUKTURU SOUSEDNOSTI)
C     -------------------------------------------------
      MAXTMP = GTINTR(MAXM)
      CALL GTPERM(POCNEZ,M(IMADJ),M(IMADJ),M(TMPADJ),M(TMPADJ),DEFTR,
     /            M(IMPERM),M(IMINVP),M(IMFREE),MAXTMP,OUTP)
      CALL GTFREI(-MAXADJ,IMADJ,IMFREE,MAXM,OUTP)
      IOUT = OUTP - 30
      IF(OUTP.NE.0)  RETURN
C     --------------------------------------------------------------
C     SYMBOLICKY CHOLESKYHO ROZKLAD (STRUKTURA SOUSEDNOSTI OBNOVENA)
C     --------------------------------------------------------------
      IMADJ = TMPADJ
      MAXNZS = GTRINT(MAXM)
      CALL GTSCHR(POCNEZ,M(IMADJ),M(IMADJ),M(IMPERM),M(IMINVP),
     /            M(IMXRNZ),M(IMXNZS),M(IMFREE),MAXNZS,POCNEP,OUTP)
      MAXM = MAXM + RPOC
      CALL GTFREI(MAXNZS,IMNZS ,IMFREE,MAXM,OUTP)
      IOUT = OUTP - 40
      IF(OUTP.NE.0)  RETURN
      CALL GTTIME
C     ----------------------
C     GIVENSOVA TRANSFORMACE
C     ----------------------
      CALL GTDIMR(M(IMXRNZ),POCNEZ,DIMROW)
      CALL GTFREI(DIMROW,IMINDC,IMFREE,MAXM,OUTP)
      CALL GTFREE(DIMROW,IMROW ,IMFREE,MAXM,OUTP)
      CALL GTFREE(POCNEZ,IMDIAG,IMFREE,MAXM,OUTP)
      CALL GTFREE(POCNEZ,IMRAD ,IMFREE,MAXM,OUTP)
      CALL GTFREI(POCNEZ,IMRMAS,IMFREE,MAXM,OUTP)
      CALL GTFREE(POCPOZ,IMVAHA,IMFREE,MAXM,OUTP)
      CALL GTFREE(POCNEP,IMRNEP,IMFREE,MAXM,OUTP)
      IOUT = OUTP - 50
      IF(OUTP.NE.0)  RETURN
      MAXTMP = GTRINT(MAXM)
      CALL GTRANS(LUN,POCNEZ,POCPOZ,POCNEP,M(IMDIAG),M(IMRNEP),M(IMX),
     /            M(IMINVP),TOLMIN,TOLEPS,M(IMVAHA),M(IMRAD),M(IMRMAS),
     /            M(IMXRNZ),M(IMNZS),M(IMXNZS),M(IMINDC),M(IMROW),
     /            M0,DEFEKT,M(IMFREE),MAXTMP,OUTP)
C
      IOUT = 0
      IF(MODE.EQ.1)  RETURN
2     CONTINUE
C
      CALL GTFREI(DEFEKT,IMDFSZ,IMFREE,MAXM,OUTP)
      MDFMK = POCNEZ
      NDFMK = DEFEKT + 1
      K = MDFMK*NDFMK
      CALL GTFREE(K,IMDFMK,IMFREE,MAXM,OUTP)
      IOUT = OUTP - 60
      IF(OUTP.NE.0)  RETURN
C
C     ------------------------------
C     ZPRACOVANI SINGULARNI SOUSTAVY
C     ------------------------------
      IF(DEFEKT.EQ.0)  GOTO 699
         CALL GTFREI(POCNEZ,INTMP ,IMFREE,MAXM,OUTP)
         IOUT = OUTP - 60
         IF(OUTP.NE.0)  RETURN
         CALL GTSING(POCNEZ,DEFEKT,M(IMDFSZ),M(IMDFMK),MDFMK,NDFMK,
     /               M(INTMP),NX,INDX,TOLMIN,
     /               M(IMDIAG),M(IMX),M(IMXRNZ),M(IMXNZS),M(IMNZS),
     /               M(IMRNEP),IDF)
         CALL GTFREI(-POCNEZ,INTMP ,IMFREE,MAXM,OUTP)
699   CONTINUE
C     ----------------------------
C     INVERZNI PERMUTACE VEKTORU X
C     ----------------------------
      CALL GTPERV(POCNEZ,M(IMX),M(IMRAD),M(IMINVP))
      MAXM = IMFREE - 1
      IOUT = 0
      CALL GTTIME
      RETURN
C
      END
