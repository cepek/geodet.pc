C PERMUTACE NEZNAMYCH - MINIMUM DEGRE ORDERING
C=======================================================================
C
      SUBROUTINE GTPERM(POCNEZ,XADJ,ADJNCY,XAD2,ADJNC2,DEFTR,
     /                  PERM,INVP,TMP,DIMTMP,IOUT)
C
C POCNEZ  POCET NEZNAMYCH
C (XADJ, ADJNCY) STRUKTURA SOUSEDNOSTI MATICE TRANS(A)*A - VIZ SPARSPAK
C         !!! POZOR !!!  PO NAVRATU Z PROCEDURY JE STRUKTURA ZNICENA
C (XAD2, ADJNC2) STRUKTURA SOUSEDNOSTI - ZUSTAVA PROCEDUROU NEZMENENA !
C DEFTR   POCET NEZNAMYCH PRO TRANSFORMACI SINGULARNI SOUSTAVY
C PERM    VEKTOR PERMUTACE NEZNAMYCH
C INVP    INVERZNI PERMUTACE
C TMP     PRACOVNI PROSTOR
C DIMTMP  DIMENZE PRACOVNIHO PROSTORU
C IOUT    VYSTUPNI PARAMETR:
C         IOUT =  0  V POLI NZSUB JSOU ULOZENY KOMPRIMOVANE INDEXY 
C         IOUT = -1  NEDOSTATECNA KAPACITA PAMETI (MAXNZS)
C
      INTEGER  POCNEZ, XADJ(1), ADJNCY(1), XAD2(1), ADJNC2(1), DEFTR, 
     /         PERM(1), INVP(1), TMP(1), DIMTMP, IOUT
      INTEGER  DEG, MARKER, RCHSET, NBRHD, QSIZE, QLINK, NOFSUB,
     /         TMPFRE, TMPMAX
      INTEGER  ROOT, NLVL, ILS, DTR1, DTR2, I, K
C
C     -----------------------
C     ALOKACE PRACOVNICH POLI
C     -----------------------
      TMPFRE = 1
      TMPMAX = DIMTMP
      CALL GTFREE(POCNEZ,DEG   ,TMPFRE,TMPMAX,IOUT)
      CALL GTFREE(POCNEZ,MARKER,TMPFRE,TMPMAX,IOUT)
      CALL GTFREE(POCNEZ,QSIZE ,TMPFRE,TMPMAX,IOUT)
      CALL GTFREE(POCNEZ,QLINK ,TMPFRE,TMPMAX,IOUT)
      CALL GTFREE(POCNEZ,RCHSET,TMPFRE,TMPMAX,IOUT)
      CALL GTFREE(POCNEZ,NBRHD ,TMPFRE,TMPMAX,IOUT)
      IF(IOUT.NE.0)  RETURN
C     ---------------------------------
C     QUOTIENT MINIMUM DEGREE ALGORITHM
C     ---------------------------------
      CALL GENQMD(POCNEZ,XADJ,ADJNCY,PERM,INVP,
     /            TMP(DEG),TMP(MARKER),TMP(RCHSET),TMP(NBRHD),
     /            TMP(QSIZE),TMP(QLINK),NOFSUB)
      IF(DEFTR.LE.1)  RETURN
C     --------------------------------------------
C     ROOTED LEVEL STRUCTURE / ROOT = PERM(POCNEZ)
C     --------------------------------------------
      ROOT = PERM(POCNEZ)
      DO 10 I=1,POCNEZ
         TMP(MARKER+I-1) = 1
   10 CONTINUE
      CALL ROOTLS(ROOT,XAD2,ADJNC2,TMP(MARKER),NLVL,
     /            TMP(QSIZE),TMP(QLINK))
C     -----------------------------------------------------------
C     DEFTR/2  NEZNAMYCH Z POSLEDNI UROVNE ROOTED LEVEL STRUCTURE
C     ZARAZENO V PERM PRED POSLEDNI NEZNAMOU
C     -----------------------------------------------------------
      IF(NLVL.LT.DEFTR)  RETURN
      DTR1 = DEFTR/2
      DTR2 = DEFTR - DTR1
      DO 20 I=1,DTR1
         ILS = TMP(QLINK+POCNEZ-I)
         K   = INVP(ILS)
         PERM(K) = 0
   20 CONTINUE
      K = 0
      DO 30 I=1,POCNEZ-DTR2
         IF(PERM(I).EQ.0)  GOTO 30
            K = K + 1
            PERM(K) = PERM(I)
   30 CONTINUE
      K = POCNEZ - DTR2 + 1
      DO 40 I=1,DTR1
         ILS = TMP(QLINK+POCNEZ-I)
         K   = K - 1
         PERM(K) = ILS
   40 CONTINUE
C     -------------------------------
C     NOVY VYPOCET INVERZNI PERMUTACE 
C     -------------------------------
      DO 50 I=1,POCNEZ
         INVP(PERM(I)) = I
   50 CONTINUE
      RETURN
C
      END
