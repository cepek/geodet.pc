C VYPOCET VAHOVYCH KOEFICIENTU 
C=======================================================================
C
      SUBROUTINE GTVAHK(MODE,LUN1,LUN2,LUNTMP,TOLMIN,
     /                  M,MAXM,NX,INDX,W,MT,MTDIM,IOUT)
C
C MODE    MODE = 1  VYPOCET VAHOVYCH KOEFICIENTU VYROVNANYCH POZOROVANI
C         MODE = 2  VYPOCET VAHOVYCH KOEFICIENTU VYROVNANYCH NEZNAMYCH
C         MODE = 3  VYPOCET VAHOVYCH KOEFICIENTU POZOROVANI A NEZNAMYCH
C LUN1    VSTUPNI ZARIZENI PRO CTENI MATICE KOEFICIENTU ROVNIC OPRAV
C LUN2    VYSTUPNI ZARIZENI PRO ZAPIS MATICE VAHOVYCH KOEFICIENTU
C LUNTMP  PRACOVNI ZARIZENI PRO PREVOD VAHOVYCH KOEFICIENTU NA ZAPIS
C         PO RADCICH
C TOLMIN  TOLERANCE PRO IDENTIFIKACI LINEARNE ZAVISLYCH SLOUPCU
C M       PRACOVNI PROSTOR, VE KTEREM BYLA ALOKOVANA VSECHNA POTREBNA
C         PRACOVNI POLE (VIZ GTIMPT.INC)
C MAXM    DIMENZE POLE M
C NX      POCET PRVKU SUBVEKTORU NEZNAMYCH, JEHOZ DELKA MA BYT
C         MINIMALIZOVANA
C INDX    POLE PRO REGISTRACI INDEXU NEZNAMYCH, KTERE VSTUPUJI DO
C         MINIMALIZACNI PODMINKY V PRIPADE NENULOVEHO DEFEKTU
C W       PRACOVNI VEKTOR (DIMENZE POCNEZ)
C MT      PRACOVNI POLE PRO PREVOD MATICE VAHOVYCH KOEFICIENTU
C         NEZNAMYCH ZAPSANYCH PO SLOUPCICH NA ZAPIS PO RADCICH
C MTDIM   DIMENZE POLE MT
C IOUT    VYSTUPNI PARAMETR
C         IOUT =  0   REGULARNI UKONCENI
C         IOUT = -2   CHYBA PRI CTENI VSTUPNIHO SOUBORU
C         IOUT = -3   CHYBA PRI ZAPISU DO VYSTUPNIHO SOUBORU
C         IOUT = -4   CHYBA PRI CTENI/ZAPISU PRACOVNIHO SOUBORU
C
      INTEGER  MODE, LUN1, LUN2, LUNTMP, MAXM, NX, INDX(1), MTDIM, IOUT
C$INCLUDE: 'GTREAL.INC'
      DOUBLE PRECISION
     /         TOLMIN, M(MAXM), W(1), MT(1)
C
C$INCLUDE: 'GTIMPT.INC'
      INTEGER         
     /   IMX   , IMADJ , IMPERM, IMINVP, IMXRNZ, IMXNZS, IMNZS , IMDIAG,
     /   IMRAD , IMRMAS, IMVAHA, IMRNEP, IMINDC, IMROW , IMDFSZ, 
     /   IMDFMK, MDFMK , NDFMK ,
     /   IMFREE, IMMAXR, IMMAXI
      COMMON /GTIMPT/
     /   IMX   , IMADJ , IMPERM, IMINVP, IMXRNZ, IMXNZS, IMNZS , IMDIAG,
     /   IMRAD , IMRMAS, IMVAHA, IMRNEP, IMINDC, IMROW , IMDFSZ, 
     /   IMDFMK, MDFMK , NDFMK ,
     /   IMFREE, IMMAXR, IMMAXI
C
C-----------------------------------------------------------------------
C
      INTEGER  POCNEZ, POCPOZ, MTZAZ
C
      CALL GTGET0(LUN1,POCNEZ,POCPOZ,IOUT)
      IF(IOUT.NE.0)  RETURN
      MTZAZ = MTDIM/POCNEZ
      CALL GTVHK1(MODE,POCNEZ,TOLMIN,M(IMDFMK),MDFMK,NDFMK,NX,INDX,
     /            M(IMDIAG),M(IMXRNZ),M(IMXNZS),M(IMNZS),
     /            M(IMRNEP),M(IMPERM),M(IMINVP),
     /            LUN1,LUN2,LUNTMP,W,MT,MTZAZ,IOUT)
C
      RETURN
C
      END
C VYPOCET VAHOVYCH KOEFICIENTU - VYKONNY MODUL
C=======================================================================
C
      SUBROUTINE GTVHK1(MODE,POCNEZ,TOLMIN,DFMK,MDFMK,NDFMK,NX,INDX,
     /                  DIAG,XRNZ,XNZSUB,NZSUB,RNEP,PERM,INVP,
     /                  LUN1,LUN2,LUNTMP,WX,MT,MTZAZ,IOUT)
C
C MODE    MODE = 1  VYPOCET VAHOVYCH KOEFICIENTU VYROVNANYCH POZOROVANI
C         MODE = 2  VYPOCET VAHOVYCH KOEFICIENTU VYROVNANYCH NEZNAMYCH
C         MODE = 3  VYPOCET VAHOVYCH KOEFICIENTU POZOROVANI A NEZNAMYCH
C POCNEZ  POCET NEZNAMYCH
C TOLMIN  TOLERANCE PRO IDENTIFIKACI LINEARNE ZAVISLYCH SLOUPCU
C DFMK    MATICE K - RESENI SINGULARNI SOUSTAVY
C MDFMK   POCET RADKU MATICE DFMK (POCNEZ)
C NDFMK   POCET SLOUPCU MATICE DFMK (DEFEKT+1)
C NX      POCET PRVKU SUBVEKTORU NEZNAMYCH, JEHOZ DELKA MA BYT
C         MINIMALIZOVANA
C INDX    POLE PRO REGISTRACI INDEXU NEZNAMYCH, KTERE VSTUPUJI DO
C         MINIMALIZACNI PODMINKY V PRIPADE NENULOVEHO DEFEKTU
C DIAG    DIAGONALNI PRVKY MATICE R
C XRNZ    VEKTOR SMERNIKU NA SEZNAMY NENULOVYCH PRVKU V RADCICH R
C XNZSUB  VEKTOR SMERNIKU DO NZSUB
C NZSUB   KOMPRIMOVANY VEKTOR INDEXU NENULOVYCH PRVKU R
C RNEP    NEDIAGONALNI NENULOVE PRVKY ROZKLADU R
C PERM    VEKTOR PERMUTACE NEZNAMYCH
C INVP    INVERZNI PERMUTACE
C LUN1    VSTUPNI ZARIZENI PRO CTENI MATICE KOEFICIENTU ROVNIC OPRAV
C LUN2    VYSTUPNI ZARIZENI PRO ZAPIS MATICE VAHOVYCH KOEFICIENTU
C LUNTMP  PRACOVNI ZARIZENI PRO PREVOD VAHOVYCH KOEFICIENTU NA ZAPIS
C         PO RADCICH
C WX      PRACOVNI VEKTOR (DIMENZE POCNEZ)
C MT      PRACOVNI MATICE PRO PREVOD MATICE VAHOVYCH KOEFICIENTU
C         NEZNAMYCH ZAPSANYCH PO SLOUPCICH NA ZAPIS PO RADCICH
C MTZAZ   POCET RADKU MATICE MT (POCET SLOUPCU JE POCNEZ)
C IOUT    VYSTUPNI PARAMETR
C         IOUT =  0   REGULARNI UKONCENI
C         IOUT = -2   CHYBA PRI CTENI VSTUPNIHO SOUBORU
C         IOUT = -3   CHYBA PRI ZAPISU DO VYSTUPNIHO SOUBORU
C         IOUT = -4   CHYBA PRI CTENI/ZAPISU PRACOVNIHO SOUBORU
C
      INTEGER  MODE, POCNEZ, MDFMK, NDFMK, NX, INDX(1),
     /         XRNZ(1), XNZSUB(1), NZSUB(1), PERM(1), INVP(1),
     /         LUN1, LUN2, LUNTMP, MTZAZ, IOUT
C$INCLUDE: 'GTREAL.INC'
      DOUBLE PRECISION
     /         TOLMIN, DFMK(MDFMK,NDFMK), DIAG(1), WX(1), RNEP(1),
     /         MT(MTZAZ,POCNEZ)
C
      INTEGER  NZNM, IDF, IZAZN, I, J, K
      INTEGER    MAXSLP
      PARAMETER (MAXSLP=100)
      INTEGER    INDC(MAXSLP), NONZ, POCMER, KSTRT, KSTOP, KSUB, ITER,
     /           MTPOC, IRAD
C$INCLUDE: 'GTREAL.INC'
      DOUBLE PRECISION
     /           ROW(MAXSLP), W, ABSCL
C
C     --------------------------------------------------
C     VYPOCET VAHOVYCH KOEFICIENTU VYROVNANYCH NEZNAMYCH
C     --------------------------------------------------
      CALL GTTIME
      IF(MODE.EQ.2)  GOTO 199
      DO 100 NZNM=1,POCNEZ
         DO 110 J=1,POCNEZ
            DFMK(J,NDFMK) = 0.0
110      CONTINUE
         DFMK(NZNM,NDFMK) = 1.0
         CALL GTRZS1(POCNEZ,TOLMIN,DIAG,DFMK(1,NDFMK),
     /               XRNZ,XNZSUB,NZSUB,RNEP)
         IF(NDFMK.GT.1)
     /      CALL GTORTM(DFMK,MDFMK,NDFMK,NDFMK-1,NX,INDX,TOLMIN,IDF)
         DO 200 J=1,POCNEZ
            K = INVP(J)
            WX(J) = DFMK(K,NDFMK)
200      CONTINUE
         WRITE(LUNTMP,REC=NZNM,ERR=904) (WX(J),J=1,POCNEZ)
100   CONTINUE
199   CONTINUE
C
C     ---------------------------------------------------
C     VYPOCET VAHOVYCH KOEFICIENTU VYROVNANYCH POZOROVANI
C     ---------------------------------------------------
      CALL GTTIME
      IF(MODE.EQ.1)  GOTO 399
      IZAZN = 0
      CALL GTGET0(LUN1,POCNEZ,POCMER,IOUT)
      IF(IOUT.NE.0)   RETURN
300   CONTINUE
         CALL GTGETR(LUN1,NONZ,INDC,ROW,ABSCL,W,IOUT)
         IF(IOUT.EQ.-1)  GOTO 399
         IF(IOUT.NE.0)   RETURN
         DO 310 I=1,POCNEZ
            WX(I) = 0.0
310      CONTINUE
         W = SQRT(W)
         DO 320 I=1,NONZ
            K = INDC(I)
            K = INVP(K)
            WX(K) = ROW(I)/W
320      CONTINUE
         DO 400 I=1,POCNEZ
            IF(ABS(DIAG(I)).GT.TOLMIN)  GOTO 500
               WX(I) = 0.0
               GOTO 400
500         CONTINUE
            WX(I) = WX(I)/DIAG(I)
            KSTRT = XRNZ(I)
            KSTOP = XRNZ(I+1) - 1
            IF(KSTRT.GT.KSTOP)  GOTO 400
               KSUB  = XNZSUB(I)
               DO 510 K=KSTRT,KSTOP
                  J = NZSUB(KSUB)
                  WX(J) = WX(J) - RNEP(K)*WX(I)
                  KSUB = KSUB + 1
510            CONTINUE
400      CONTINUE
         IZAZN = IZAZN + 1
         WRITE(LUN2,REC=IZAZN,ERR=903)  (WX(I),I=1,POCNEZ)
         GOTO 300
399   CONTINUE
C
C     -----------------------------------------------
C     ZAPIS VAHOVYCH KOEFICIENTU NEZNAMYCH PO RADCICH
C     -----------------------------------------------
      CALL GTTIME
      IF(MODE.EQ.2)  GOTO 699
      ITER  = (POCNEZ+MTZAZ-1)/MTZAZ
      IRAD  = 0
      MTPOC = MTZAZ
      DO 600 I=1,ITER
         IF(IRAD+MTZAZ.GT.POCNEZ)  MTPOC = POCNEZ - IRAD
         DO 710 K=1,POCNEZ
            READ(LUNTMP,REC=K,ERR=904)  (WX(J),J=1,POCNEZ)
            DO 700 J=1,MTPOC
               MT(J,K) = WX(IRAD+J)
700         CONTINUE
710      CONTINUE
         IRAD = IRAD + MTPOC
709      CONTINUE
         DO 800 J=1,MTPOC
            IZAZN = IZAZN + 1
            WRITE(LUN2,REC=IZAZN,ERR=903)  (MT(J,K),K=1,POCNEZ)
800      CONTINUE
600   CONTINUE
699   CONTINUE
      CALL GTTIME
      IOUT =  0
      RETURN
903   IOUT = -3
      RETURN
904   IOUT = -4
      RETURN
C
      END
