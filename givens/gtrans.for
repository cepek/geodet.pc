C GIVENSOVA TRANSFORMACE
C=======================================================================
C
      SUBROUTINE GTRANS(LUN,POCNEZ,POCPOZ,POCNEP,DIAG,RNEP,X,INVP,
     /                  TOLMIN,TOLEPS,VAHA,RADEK,RMASK,XRNZ,NZSUB,
     /                  XNZSUB,INDC,ROW,M0,DEFEKT,DEFSEZ,DIMDFS,IOUT)
C
C LUN     CISLO LIGICKEHO ZARIZENI PRO CTENI MATICE A (A*X=B)
C POCNEZ  POCET NEZNAMYCH  (SLOUPCU MATICE A)
C POCPOZ  POCET POZOROVANI (RADKU   MATICE A)
C POCNEP  POCET NENULOVYCH PRVKU ROZKLADU R (CHOL. ROZKLAD TRANS(A)*A)
C DIAG    DIAGONALNI PRVKY MATICE R
C RNEP    NEDIAGONALNI NENULOVE PRVKY ROZKLADU R
C X       VEKTOR NEZNAMYCH X
C INVP    VEKTOR INVERZNI PERMUTACE NEZNAMYCH
C TOLMIN  TOLERANCE PRO IDENTIFIKACI LINEARNE ZAVISLYCH SLOUPCU
C         (HODNOTY PRVKU HLAVNI DIAGONALY, RESP. NORMY VEKTORU
C         PRI RESENI SINGULARNI SOUSTAVY)
C TOLEPS  CHARAKTERISTIKA SKUTECNE PRESNOSTI PRVKU MATICE SOUSTAVY
C         (TOLERANCE PRO IDENTIFIKACI NULOVYCH PRVKU PRI VYPOCTU 
C         TRANSFORMACNICH KOEFICIENTU)
C VAHA    VAHY POZOROVANI (RADKU MATICE A)
C RADEK   PRACOVNI POLE PRO ULOZENI BEZNEHO RADKU (MATICE A)
C XRNZ    VEKTOR SMERNIKU NA SEZNAMY NENULOVYCH PRVKU V RADCICH R
C NZSUB   KOMPRIMOVANY VEKTOR INDEXU NENULOVYCH PRVKU R
C XNZSUB  VEKTOR SMERNIKU DO NZSUB
C INDC    PRAC. POLE PRO ULOZENI INDEXU NENULOVYCH PRVKU BEZNEHO RADKU
C ROW     PRACOVNI POLE PRO ULOZENI BEZNEHO RADKU
C M0      EMPIRICKA JEDNOTKOVA STREDNI CHYBA
C DEFEKT  DEFEKT SOUSTAVY
C DEFSEZ  SEZNAM INDEXU LINEARNE ZAVISLYCH SLOUPCU
C DIMDFS  DIMENZE SEZNAMU DEFSEZ
C IOUT    VYSTUPNI PARAMETR:
C         IOUT =  0  REGULARNI UKONCENI CINNOSTI
C         IOUT = -1  NEDOSTATECNA KAPACITA PAMETI
C         IOUT = -2  CHYBA PRI CTENI MATICE A
C         
      INTEGER  LUN, POCNEZ, POCPOZ, POCNEP, INVP(1), XRNZ(1), NZSUB(1), 
     /         XNZSUB(1), INDC(1), RMASK(1), DEFEKT, DEFSEZ(1), DIMDFS, 
     /         IOUT
C$INCLUDE: 'GTREAL.INC'
      DOUBLE PRECISION
     /         DIAG(1), RNEP(1), X(1), TOLMIN, TOLEPS, VAHA(1),
     /         RADEK(1), ROW(1), M0
C
C-----------------------------------------------------------------------
C
      INTEGER  NONZ, POC, KON, IND, I, J, NADB
C$INCLUDE: 'GTREAL.INC'
      DOUBLE PRECISION
     /         ABSCL, W, PVV
C
C     ------------
C     INICIALIZACE
C     ------------
      DO 100 I=1,POCNEZ
         DIAG(I)  = 0.0
         RADEK(I) = 0.0
         RMASK(I) =  0
100   CONTINUE
      DO 101 I=1,POCNEZ
         X(I) = 0.0
101   CONTINUE
      DO 102 I=1,POCNEP
         RNEP(I) = 0.0
102   CONTINUE
      CALL GTGET0(LUN,POCNEZ,POCPOZ,IOUT)
      IF(IOUT.NE.0)  RETURN
      PVV = 0.0
C     -------------------------------
C     ZPRACOVANI VSECH RADKU MATICE A
C     -------------------------------
      DO 200 I=1,POCPOZ
         CALL GTGETR(LUN,NONZ,INDC,ROW,ABSCL,W,IOUT)
         IF(IOUT.NE.0)  RETURN
         VAHA(I) = W
C        -----------------------------------
C        INVERZNI PERMUTACE INDEXU NEZNAMYCH
C        -----------------------------------
         DO 202 J=1,NONZ
            INDC(J) = INVP(INDC(J))
202      CONTINUE
C        -------------------------
C        ZPRACOVANI RADKU MATICE A
C        -------------------------
         CALL GTRAD1(NONZ,INDC,ROW,ABSCL,
     /               POCNEZ,RMASK,TOLEPS,DIAG,X,RADEK,
     /               XRNZ,XNZSUB,NZSUB,RNEP)
         PVV = PVV + ABSCL**2
200   CONTINUE
C     ----------------------------------------------
C     URCENI DEFEKTU, ANULOVANI LIN. ZAVISLYCH RADKU
C     ----------------------------------------------
      DEFEKT = 0
      DO 500 I=1,POCNEZ
         IF(ABS(DIAG(I)).GT.TOLMIN)  GOTO 500
            DEFEKT = DEFEKT + 1
            IF(DEFEKT.LE.DIMDFS)  DEFSEZ(DEFEKT) = I
            ABSCL = X(I)
            X(I) = 0.0
            POC = XRNZ(I)
            KON = XRNZ(I+1) - 1
            IF(POC.GT.KON)  GOTO 400
               IND = XNZSUB(I)               
               NONZ = 0
               DO 300 J=POC,KON
                  NONZ = NONZ + 1
                  ROW(NONZ)  = RNEP(J)
                  RNEP(J)    = 0.0
                  INDC(NONZ) = NZSUB(IND)
                  IND = IND + 1
300            CONTINUE
               CALL GTRAD1(NONZ,INDC,ROW,ABSCL,
     /                     POCNEZ,RMASK,TOLEPS,DIAG,X,RADEK,
     /                     XRNZ,XNZSUB,NZSUB,RNEP)
400         CONTINUE
            PVV = PVV + ABSCL**2
500   CONTINUE
      M0 = 0.0
      NADB = POCPOZ-POCNEZ+DEFEKT
      IF(NADB.GT.0)  M0 = SQRT(PVV/NADB)
C     -------------------------
C     VYPOCET ZPETNE SUBSTITUCE
C     -------------------------
      CALL GTRZS1(POCNEZ,TOLMIN,DIAG,X,XRNZ,XNZSUB,NZSUB,RNEP)
C
      IOUT = 0
      IF(DEFEKT.GT.DIMDFS)  IOUT = -1
      RETURN
C
      END
