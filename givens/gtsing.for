C GIVENSOVA TRANSFORMACE - ZPRACOVANI SINGULARNI SOUSTAVY
C=======================================================================
C
      SUBROUTINE GTSING(POCNEZ,DEFEKT,DEFSEZ,DFMK,MDFMK,NDFMK,MARK,NX,
     /                  INDX,TOLMIN,DIAG,X,XRNZ,XNZSUB,NZSUB,RNEP,IDF)
C
C POCNEZ  POCET NEZNAMYCH  (SLOUPCU MATICE A)
C DEFEKT  POCET LINEARNE ZAVISLYCH SLOUPCU
C DEFSEZ  SEZNAM INDEXU LINEARNE ZAVISLYCH SLOUPCU
C DFMK    MATICE K - RESENI SINGULARNI SOUSTAVY
C MDFMK   POCET RADKU MATICE DFMK (POCNEZ)
C NDFMK   POCET SLOUPCU MATICE DFMK (DEFEKT+1)
C MARK    PRACOVNI POLE
C NX      POCET PRVKU SUBVEKTORU NEZNAMYCH, JEHOZ DELKA MA BYT
C         MINIMALIZOVANA
C INDX    POLE PRO REGISTRACI INDEXU NEZNAMYCH, KTERE VSTUPUJI DO
C         MINIMALIZACNI PODMINKY V PRIPADE NENULOVEHO DEFEKTU
C TOLMIN  TOLERANCE PRO IDENTIFIKACI LINEARNE ZAVISLYCH SLOUPCU
C DIAG    DIAGONALNI PRVKY MATICE R
C X       VEKTOR NEZNAMYCH X
C XRNZ    VEKTOR SMERNIKU NA SEZNAMY NENULOVYCH PRVKU V RADCICH R
C XNZSUB  VEKTOR SMERNIKU DO NZSUB
C NZSUB   KOMPRIMOVANY VEKTOR INDEXU NENULOVYCH PRVKU R
C RNEP    NEDIAGONALNI NENULOVE PRVKY ROZKLADU R
C IDF     DEFEKT SINGULARNI SOUSTAVY
C
      INTEGER  POCNEZ, DEFEKT, DEFSEZ(1), MDFMK, NDFMK, MARK(1),
     /         NX, INDX(1), XRNZ(1), XNZSUB(1), NZSUB(1), IDF
C$INCLUDE: 'GTREAL.INC'
      DOUBLE PRECISION
     /         DFMK(MDFMK,NDFMK), TOLMIN, DIAG(1), X(1), RNEP(1)
C
C-----------------------------------------------------------------------
C
      INTEGER  POC, KON, IND, JJ, I, J, K
C
      DO 200 I=1,POCNEZ
         DO 100 J=1,DEFEKT
            DFMK(I,J) = 0.0
100      CONTINUE
         DFMK(I,DEFEKT+1) = X(I)
         MARK(I) = 0
200   CONTINUE
      DO 300 I=1,DEFEKT
         K = DEFSEZ(I)
         MARK(K) = I
300   CONTINUE
      DO 500 I=1,POCNEZ
         POC = XRNZ(I)
         KON = XRNZ(I+1) - 1
         IF(POC.GT.KON)  GOTO 500
            IND = XNZSUB(I)               
            DO 400 JJ=POC,KON
               J = NZSUB(IND)
               IND = IND + 1
               K = MARK(J)
               IF(K.EQ.0)  GOTO 400
                  DFMK(I,K) = -RNEP(JJ)
400         CONTINUE
500   CONTINUE
      DO 600 I=1,DEFEKT
        CALL GTRZS1(POCNEZ,TOLMIN,DIAG,DFMK(1,I),XRNZ,XNZSUB,NZSUB,RNEP)
600   CONTINUE
      DO 550 I=1,DEFEKT
         K = DEFSEZ(I)
         DFMK(K,I) = 1.0
550   CONTINUE
      CALL GTORTM(DFMK,MDFMK,NDFMK,0,NX,INDX,TOLMIN,IDF)
      DO 700 I=1,POCNEZ
         X(I) = DFMK(I,NDFMK)
700   CONTINUE
      RETURN
C
      END
